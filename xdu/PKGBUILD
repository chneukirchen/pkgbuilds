# Maintainer: Christian Neukirchen <chneukirchen@gmail.com>
pkgname=xdu
pkgver=3.0
pkgrel=2
pkgdesc="display the output of du(1) in an X window"
arch=('i686' 'x86_64')
url="http://web.archive.org/web/20131111062856/http://sd.wareonearth.com/~phil/xdu/"
license=(custom)
depends=(libxaw)
makedepends=(imake)
source=("http://ftp.de.debian.org/debian/pool/main/x/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz" LICENSE)
md5sums=('630f11f18b0f1aa074f8f53ff68f2be4'
         '55cab0476b19278fd26d5e8db6946703')
sha256sums=('42c018597e31fade56e44b21ed7041f23756532df6e114b5475b4a50cd4df881'
            '81b391758c8346db3c4db638c0656b13d64c909411ecb4db20110a5de32fdfe7')

build() {
  cd "$srcdir"
  xmkmf
  make clean
  make
}

package() {
  cd "$srcdir"
  install -D -m755 xdu "${pkgdir}/usr/bin/xdu"
  install -D -m644 xdu.man "${pkgdir}/usr/share/man/man1/xdu.1"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
