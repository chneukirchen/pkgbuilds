# Maintainer: Christian Neukirchen <chneukirchen@gmail.com>
pkgname=ded
pkgver=20120113
_td_pkgver=20120904
pkgrel=2
pkgdesc="directory editor, file manager similar to Emacs dired"
arch=('i686' 'x86_64')
url="http://invisible-island.net/ded/ded.html"
license=('custom:BSD')
depends=('ncurses')
source=(ftp://invisible-island.net/ded/ded.tar.gz 
        ftp://invisible-island.net/ded/td_lib.tar.gz)
noextract=()
md5sums=('302f6c534e50fd90b4428e32a846744e'
         'de01c983736602d63c47800cc0e25c16')
sha256sums=('f6625db6dddcedae9e33fb5c7e88dc726dd6c910a09f1b98e6e2136ce91ca5a5'
            '60d89c68ce0b745ea421e670d8e36fb63c396a195564da09fa549052ff001e76')

build() {
  cd "$srcdir/td_lib-$_td_pkgver"
  ./configure --prefix=/usr
  make -j1

  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
