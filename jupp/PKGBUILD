# Maintainer: Christian Neukirchen <chneukirchen@gmail.com>
# Contributor: JohannesSM64 <johannes.a.langoy@gmail.com>
pkgname=jupp
pkgver=3.1jupp23
pkgrel=1
pkgdesc="An enhanced, portable version of joe, forked in 2004."
url="https://mirbsd.org/jupp.htm"
license=('custom:GPLv1')
depends=('glibc' 'ncurses')
arch=('i686' 'x86_64')
conflicts=('joe')
source=("http://www.mirbsd.org/MirOS/dist/jupp/joe-$pkgver.tgz")

md5sums=('96bcaaa4d17c0a99a76d287a0237e1fa')
sha256sums=('7f8b2e76ebb39544f5bbca2e4f7463f2afb685bc11f16603163acc3e8af0bae1')

backup=('etc/joe/jupprc' 
        'etc/joe/joerc' 
	'etc/joe/jmacsrc'
	'etc/joe/rjoerc'
	'etc/joe/jstarrc'
	'etc/joe/jpicorc')

build() {
	cd "$srcdir/$pkgname"
	
	sh configure --prefix=/usr --sysconfdir=/etc \
	             --disable-dependency-tracking --disable-termidx
	make
}

package() {
	cd "$srcdir/$pkgname"
	make "DESTDIR=$pkgdir" install
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
