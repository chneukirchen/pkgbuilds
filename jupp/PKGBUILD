# Maintainer: Christian Neukirchen <chneukirchen@gmail.com>
# Contributor: JohannesSM64 <johannes.a.langoy@gmail.com>
pkgname=jupp
pkgver=3.1jupp24
pkgrel=1
pkgdesc="An enhanced, portable version of joe, forked in 2004."
url="https://mirbsd.org/jupp.htm"
license=('custom:GPLv1')
depends=('glibc' 'ncurses')
arch=('i686' 'x86_64')
conflicts=('joe')
source=("http://www.mirbsd.org/MirOS/dist/jupp/joe-$pkgver.tgz")

md5sums=('dbb299c31860d43acef21b709e67e9fb')
sha256sums=('3bd25ad5c9364ce0ff33ec346ec34cbeff990e5d2be35960d68d79910f98cf55')

backup=('etc/joe/jupprc' 
        'etc/joe/joerc' 
	'etc/joe/jmacsrc'
	'etc/joe/rjoerc'
	'etc/joe/jstarrc'
	'etc/joe/jpicorc')

build() {
	cd "$srcdir/$pkgname"
	
	sh configure --prefix=/usr --sysconfdir=/etc \
	             --disable-dependency-tracking --disable-termidx
	make
}

package() {
	cd "$srcdir/$pkgname"
	make "DESTDIR=$pkgdir" install
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
