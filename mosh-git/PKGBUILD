# Maintainer: Christian Neukirchen <chneukirchen@gmail.com>
pkgname=mosh-git
pkgver=20140108
pkgrel=1
pkgdesc="mobile shell (mosh) surviving disconnects with local echo and line editing"
arch=('i686' 'x86_64')
url="http://mosh.mit.edu/"
license=('GPL3')
depends=('protobuf' 'ncurses' 'zlib' 'openssh' 'perl' 'perl-io-tty')
makedepends=('git')
optdepends=('libutempter: record of session in {u,t}wmp (recompile mosh afterwards)'
            'perl-io-socket-ip: for IPv6 support'
            'vte-mosh-fix: to work properly in vte'
            'vte3-mosh-fix: to work properly in vte3')
conflicts=('mosh' 'mobile-shell-git')
source=('git+https://github.com/keithw/mosh.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/mosh"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {
  cd "$srcdir/mosh"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/mosh"
  make DESTDIR="$pkgdir/" install
  rmdir "$pkgdir/usr/etc"
}

# vim:set ts=2 sw=2 et:
