# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Renchi Raju <renchi@green.tam.uiuc.edu>

pkgname=emacs-chris2
_pkgname=emacs
pkgver=24.2
pkgrel=2
pkgdesc="The Emacs Editor, chris2 edition"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
conflicts=(emacs)
provides=(emacs)
license=('GPL3')
depends=('librsvg' 'gpm' 'giflib' 'libxpm' 'libxaw' 'hicolor-icon-theme' 'imagemagick')
install=emacs.install
source=(ftp://ftp.gnu.org/gnu/emacs/$_pkgname-$pkgver.tar.bz2{,.sig})
md5sums=('1676803a50e8adc817fdaaebb9234f14' 'ca1766337f419ef827dd96d1ff78f158')


build() {
  cd "$srcdir"/$_pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-sound --with-x-toolkit=athena --without-xaw3d --without-xft --without-gconf --without-dbus --without-gnutls --without-toolkit-scroll-bars
  make
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/$pkgver -exec chown root:root {} \;
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
}
