# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Renchi Raju <renchi@green.tam.uiuc.edu>

pkgname=emacs-chris2
_pkgname=emacs
pkgver=24.4
pkgrel=1
pkgdesc="The Emacs Editor, chris2 edition"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
conflicts=(emacs)
provides=(emacs)
license=('GPL3')
depends=('libxaw' 'hicolor-icon-theme' 'libjpeg-turbo' 'giflib' 'libpng' 'desktop-file-utils' 'perl' 'libxml2')
install=emacs.install
source=(ftp://ftp.gnu.org/gnu/emacs/$_pkgname-$pkgver.tar.xz{,.sig})
md5sums=('ad487658ad7421ad8d7b5152192eb945'
         'SKIP')
sha256sums=('47e391170db4ca0a3c724530c7050655f6d573a711956b4cd84693c194a9d4fd'
            'SKIP')

build() {
  cd "$srcdir"/$_pkgname-$pkgver
  ac_cv_lib_gif_EGifPutExtensionLast=yes ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-all --with-x-toolkit=athena \
    --with-xpm --with-jpeg --with-gif --with-png --with-xml2 \
    --with-file-notification=inotify
  make
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1,ctags.emacs.1}
  # remove conflict with texinfo
  rm "$pkgdir"/usr/share/info/info.info
  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/$pkgver -exec chown root:root {} \;
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
}
