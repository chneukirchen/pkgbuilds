# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Renchi Raju <renchi@green.tam.uiuc.edu>

pkgname=emacs-chris2
_pkgname=emacs
_majorver=23.4
pkgver=$_majorver
_realver=$_majorver
pkgrel=1
pkgdesc="The Emacs Editor, chris2 edition"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
conflicts=(emacs)
provides=(emacs)
depends=('librsvg' 'gpm'  'giflib' 'libtiff'  'libxpm' 'libjpeg' 'libxaw' 'texinfo' 'hicolor-icon-theme')
license=('GPL3')
install=emacs.install
source=(ftp://ftp.gnu.org/gnu/emacs/$_pkgname-$_realver.tar.bz2{,.sig}
        emacs-subversion17.patch)
md5sums=('070c68ad8e3c31fb3cb2414feaf5e6f0'
         '55eb16eb48b44987693c0e3ea5ab8075'
         'ac149957344de040fe30af867ac2230a')

build() {
  cd "$srcdir"/$_pkgname-$_majorver
  patch -p1 -i ../emacs-subversion17.patch
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-sound --with-x-toolkit=athena --without-xaw3d --without-xft --without-gconf --without-dbus
  make
}

package() {
  cd "$srcdir"/$_pkgname-$_majorver
  make DESTDIR="$pkgdir" install

  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1,ctags.emacs.1}
  # fix all the 777 perms on directories
  find "$pkgdir"/usr/share/emacs/$_majorver -type d -exec chmod 755 {} \;
  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/$_majorver -exec chown root:root {} \;
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
}
